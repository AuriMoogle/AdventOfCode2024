PROGRAM ReadLines
VAR CONSTANT
	MAX_LINES : __XWORD := 999;
	MAX_LINE_LENGTH : INT := 255;
END_VAR
VAR_INPUT
	FilePath : STRING;
END_VAR
VAR_OUTPUT
   Done : BOOL := FALSE;
   Error : BOOL := FALSE;
   ErrorCode : STRING := '';
   ReadLines: ARRAY[0..MAX_LINES] OF STRING(MAX_LINE_LENGTH);
   LineCount : WORD := 0;
END_VAR
VAR
	CurrentLine : STRING(MAX_LINE_LENGTH) := '';
    FileHandle: SysFile.RTS_IEC_HANDLE;
	BytesRead : __XWORD := 0;
	CharBuffer : STRING(1);
	pResult : POINTER TO SysFile.RTS_IEC_RESULT;
	i : __XWORD := 0;
	Initialized : BOOL := FALSE;
END_VAR

IF Initialized = FALSE THEN
	FOR i := 0 TO MAX_LINES DO
		ReadLines[i] := 'NOT WRITTEN TO';
	END_FOR
	Initialized := TRUE;
END_IF

FileHandle := SysFileOpen(szFile:= FilePath, am:=SysFile.AM_READ, pResult:=pResult);

IF FileHandle = SysFile.RTS_INVALID_HANDLE THEN //File not found
	ErrorCode := 'File not found, is it on the device?';
	Error := TRUE;
	Done := TRUE;
ELSE // File open
	WHILE NOT Done DO
		BytesRead := SysFileRead(hFile:=FileHandle, pbyBuffer:=ADR(CharBuffer), ulSize:=1, pResult:=pResult);

		IF BytesRead > 0 THEN
			// A little bit of cheating, only read files with unix line endings
			IF CharBuffer = '$N' THEN
				IF LineCount <= MAX_LINES THEN
					ReadLines[LineCount] := CurrentLine;
					LineCount := LineCount + 1;
					CurrentLine := '';
				ELSE
					ErrorCode := 'Input file is larger than max line count.';
					Error := TRUE;
					Done := TRUE;
				END_IF
				
			ELSE
				IF LEN(CurrentLine) <= MAX_LINE_LENGTH THEN
					CurrentLine := CONCAT(CurrentLine, CharBuffer);
				ELSE
					ErrorCode := 'Line exceedes max line length';
					Error := TRUE;
					Done := TRUE;
				END_IF
			END_IF
		ELSE
			Done := TRUE;
        	EXIT;
    	END_IF
	END_WHILE;
END_IF

SysFileClose(FileHandle);
